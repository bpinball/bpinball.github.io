    1 LET h=PEEK 23613+PEEK 23614*256: POKE h,0: POKE h+1,0
    5 LOAD ""CODE 
   10 INK 7: PAPER 0: BORDER 1: CLS 
   20 DEF FN a(x)=INT (PEEK x/16)*10+PEEK x-INT (PEEK x/16)*16
   40 PRINT #1;AT 0,28;"WAVE";
  100 GO SUB 1500
  105 RANDOMIZE USR 26368
  106 LET h=PEEK 23613+PEEK 23614*256: POKE h,0: POKE h+1,0
  110 LET sc=FN a(23429)*10000+FN a(23428)*100+FN a(23427)
  130 FOR i=1 TO 10
  140 IF sc>s(i) THEN LET n=i: GO SUB 1000: GO TO 100
  150 NEXT i
  160 GO TO 100
 1000 FOR j=9 TO i STEP -1: LET s(j+1)=s(j): LET n$(j+1)=n$(j): NEXT j
 1006 LET s(n)=sc: LET n$(n)=""
 1007 GO SUB 2000
 1010 LET x=6: LET y=n+4: GO SUB 3000: LET n$(n)=a$
 1030 IF n<>1 THEN RETURN 
 1040 PRINT #1;AT 0,10; INK 6;n$(1);
 1050 LET s=s(1): LET ch=1: LET x=13: LET y=1: GO SUB 2500
 1060 RETURN 
 1500 PRINT AT 0,12;"Robotron"
 1504 PRINT AT 1,13; INK 5;"By PAB"
 1505 PRINT AT 18,11;"GAME OVER"
 1510 GO SUB 2000
 1515 PRINT AT 20,5; INK 2;"Press any key to start";
 1520 LET c=7
 1530 PRINT BRIGHT 1;AT 0,12; INK c;"Robotron"
 1540 LET c=c-1: IF c<0 THEN LET c=7
 1550 IF INKEY$<>"" THEN RETURN 
 1560 GO TO 1530
 2000 PRINT INK 5;AT 3,8;"High score table"
 2010 FOR i=1 TO 10
 2020 PRINT INK 6;AT i+4,6;n$(i);
 2022 LET s=s(i)
 2024 LET ch=2: LET x=20: LET y=i+4: GO SUB 2500
 2030 NEXT i
 2060 RETURN 
 2500 FOR j=1 TO 6
 2510 PRINT #ch; INK 6;AT y,x+6-j;s-INT (s/10)*10;
 2520 LET s=INT (s/10)
 2530 NEXT j
 2540 RETURN 
 3000 LET a$="": LET c=0: LET mc=49
 3010 PRINT AT y,x; INK 6; FLASH 1;" ";
 3020 LET c$=INKEY$
 3021 LET mc=mc+1
 3022 IF mc=50 THEN PRINT AT 20,0;"        new high score     "
 3023 IF mc=100 THEN PRINT AT 20,0;"    please enter your name ": LET mc=0
 3030 IF c$="" THEN GO TO 3020
 3035 BEEP .01,5
 3040 IF c$=CHR$ 12 AND c>0 THEN LET c=c-1: LET x=x-1: PRINT AT y,x;"  ";: LET a$=a$(1 TO LEN a$-1): GO TO 3010
 3050 IF c$=CHR$ 13 THEN PRINT AT y,x;" ";AT 20,0;TAB 30;: RETURN 
 3060 IF c<12 AND CODE c$>31 THEN LET a$=a$+c$: PRINT AT y,x; INK 6;c$;: LET x=x+1: LET c=c+1: GO TO 3010
 3070 GO TO 3020
